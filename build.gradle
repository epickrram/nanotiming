/*
 * Copyright 2016 Mark Price
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'me.champeau.gradle.jmh' version '0.2.0'
}


apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'

defaultTasks 'checkstyleMain', 'checkstyleTest', 'build'

version = new Version(major: 0, revision: 1)

ext {
    fullName = 'Code for timing blogpost'
    fullDescription = 'Nano timing example code'
    teamName = ''
    siteUrl = 'http://lmax.com'
    sourceUrl = ''

    javaCompilerExecutable = System.env['JAVA_HOME'] ? System.env['JAVA_HOME'] + '/bin/javac' : 'javac'
}


apply plugin: 'me.champeau.gradle.jmh'

repositories {
    mavenCentral()
    maven{url 'http://jcenter.bintray.com'}
}

dependencies {
    jmh 'commons-io:commons-io:2.4'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }

    test {
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compile 'org.hdrhistogram:HdrHistogram:2.1.8'
}

jmh {
    jmhVersion = '1.11.2'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

jar {
    baseName = project.name

    manifest.attributes('Built-By': System.properties['user.name'],
                        'Bundle-Name': fullName,
                        'Bundle-Vendor': teamName,
                        'Bundle-Description': fullDescription,
                        'Bundle-DocURL': siteUrl)
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task bundleJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Resource Bundle',
                'Implementation-Version': version,
                'Main-Class': 'com.epickrram.blog.warmup.example.loop.FixedLoopCountMain'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


artifacts {
    archives sourcesJar, javadocJar
}

checkstyle {
    toolVersion = "6.3"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

class Version {
    int major, minor = 0, revision = 0
    boolean snapshot
    String stage

    String toString() {
        "$major.$minor.$revision${stage ? '.' + stage : ''}${snapshot ? '-SNAPSHOT' : ''}"
    }
}

